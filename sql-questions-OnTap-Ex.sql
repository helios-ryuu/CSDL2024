--1. Tạo database
CREATE DATABASE DETHI
GO

SET DATEFORMAT DMY;
USE QuanLyDuocPham
GO
USE DETHI;
CREATE TABLE NHACUNGCAP
(
    MANCC CHAR(5) PRIMARY KEY NOT NULL,
    TENNCC VARCHAR(25),
    QUOCGIA VARCHAR(25),
    LOAINCC VARCHAR(25)
)

CREATE TABLE DUOCPHAM
(
    MADP CHAR(4) PRIMARY KEY NOT NULL ,
    TENDP VARCHAR(25),
    LOAIDP VARCHAR(25),
    GIA INT
)

CREATE TABLE PHIEUNHAP
(
    SOPN CHAR(5) PRIMARY KEY NOT NULL ,
    NGNHAP SMALLDATETIME,
    MANCC CHAR(5),
    LOAINHAP VARCHAR(25),
    CONSTRAINT FK_PHIEUNHAP_NHACUNGCAP FOREIGN KEY (MANCC) REFERENCES NHACUNGCAP(MANCC)
)

CREATE TABLE CTPN
(
    SOPN CHAR(5),
    MADP CHAR(4),
    SOLUONG INT,
    CONSTRAINT FK_CTPN_PHIEUNHAP FOREIGN KEY (SOPN) REFERENCES PHIEUNHAP(SOPN),
    CONSTRAINT FK_CTPN_DUOCPHAM FOREIGN KEY (MADP) REFERENCES DUOCPHAM(MADP),
    PRIMARY KEY (SOPN, MADP)
)
GO

-- 2.Nhập dữ liệu cho 4 table như đề bài
INSERT INTO  NHACUNGCAP(MANCC, TENNCC, QUOCGIA, LOAINCC) VALUES ('NCC01',N'Phuc Hung',N'Viet Nam', N'Thuong xuyen')
INSERT INTO  NHACUNGCAP(MANCC, TENNCC, QUOCGIA, LOAINCC) VALUES ('NCC02',N'J. B. Pharmaceuticals',N'India', N'Vang lai')
INSERT INTO  NHACUNGCAP(MANCC, TENNCC, QUOCGIA, LOAINCC) VALUES ('NCC03',N'Sapharco',N'Singapore', N'Vang lai')

INSERT INTO DUOCPHAM(MADP, TENDP, LOAIDP, GIA) VALUES ('DP01',N'Thuoc ho PH', N'Siro', 120000)
INSERT INTO DUOCPHAM(MADP, TENDP, LOAIDP, GIA) VALUES ('DP02',N'Zecuf Herbal CouchRemedy', N'Vien nen', 200000)
INSERT INTO DUOCPHAM(MADP, TENDP, LOAIDP, GIA) VALUES ('DP03',N'Cotrim', N'Vien sui', 80000)

INSERT INTO PHIEUNHAP(SOPN, NGNHAP, MANCC, LOAINHAP) VALUES ('00001', '22/11/2017', 'NCC01', 'Noi dia')
INSERT INTO PHIEUNHAP(SOPN, NGNHAP, MANCC, LOAINHAP) VALUES ('00002', '04/12/2017', 'NCC03', 'Nhap khau')
INSERT INTO PHIEUNHAP(SOPN, NGNHAP, MANCC, LOAINHAP) VALUES ('00003', '10/12/2017', 'NCC02', 'Nhap khau')

INSERT INTO CTPN(SOPN, MADP, SOLUONG) VALUES ('00001', 'DP01', 100)
INSERT INTO CTPN(SOPN, MADP, SOLUONG) VALUES ('00001', 'DP02', 200)
INSERT INTO CTPN(SOPN, MADP, SOLUONG) VALUES ('00003', 'DP03', 543)
GO



-- 3. Tất cả các dược phẩm có loại là Siro đều có giá lớn hơn 100.000
ALTER TABLE DUOCPHAM
ADD CONSTRAINT CK_Gia_Siro
CHECK ((LOAIDP <> N'Siro') OR (GIA > 100000))
GO
-- 4. Phiếu nhập của những nhà cung cấp ở những quốc gia khác Việt Nam đều có loại nhập là Nhập khẩu.
DROP TRIGGER IF EXISTS trg_CheckPhieuNhap
GO

CREATE TRIGGER trg_CheckPhieuNhap
ON PHIEUNHAP
AFTER INSERT, UPDATE
AS
BEGIN
    -- Check phiếu nhập của những nh cung cấp khác Việt Nam đều có loại nhập là Nhập khẩu
    IF EXISTS(
        SELECT 1
        FROM inserted i
        JOIN NHACUNGCAP NCC ON i.MANCC = NCC.MANCC
        WHERE NCC.QUOCGIA <> N'Viet Nam'
        AND i.LOAINHAP <> N'Nhap khau'
    )
    --
    BEGIN
    RAISERROR(N'Phiếu nhập của những nhà cung cấp ở các quốc gia khác Việt Nam đều có loại nhập là Nhập khẩu', 16, 1);
    ROLLBACK TRANSACTION;
    END
END
GO

-- 5.
SELECT *
FROM PHIEUNHAP
WHERE ((NGNHAP >= '01/12/2017') AND  (NGNHAP <= '31/12/2017'))
ORDER BY NGNHAP ASC
GO

-- 6.
SELECT TOP 1 MADP
FROM CTPN
JOIN PHIEUNHAP P on CTPN.SOPN = P.SOPN
WHERE YEAR(NGNHAP) = 2017
ORDER BY SOLUONG DESC
GO

--7
SELECT DISTINCT DP.*
FROM DUOCPHAM DP
JOIN CTPN C on DP.MADP = C.MADP
JOIN PHIEUNHAP P on P.SOPN = C.SOPN
JOIN NHACUNGCAP N on N.MANCC = P.MANCC
WHERE N.LOAINCC = N'Thuong xuyen'
AND DP.MADP NOT IN (
    SELECT CTPN_SUB.MADP
    FROM CTPN CTPN_SUB
    JOIN PHIEUNHAP PN_SUB ON PN_SUB.SOPN = CTPN_SUB.SOPN
    JOIN NHACUNGCAP NCC_SUB ON NCC_SUB.MANCC = PN_SUB.MANCC
    WHERE NCC_SUB.LOAINCC = N'Vang lai'
    );
GO

--8
SELECT N.MANCC, N.TENNCC
FROM NHACUNGCAP N
WHERE NOT EXISTS (
    -- Tìm dược phẩm có giá > 100,000đ trong năm 2017
    SELECT DP.MADP
    FROM DUOCPHAM DP
    JOIN CTPN C ON DP.MADP = C.MADP
    JOIN PHIEUNHAP P ON P.SOPN = C.SOPN
    WHERE DP.GIA > 100000
    AND YEAR(P.NGNHAP) = 2017
    AND NOT EXISTS (
        -- Kiểm tra xem nhà cung cấp có cung cấp dược phẩm này không
        SELECT 1
        FROM CTPN C2
        JOIN PHIEUNHAP P2 ON P2.SOPN = C2.SOPN
        WHERE C2.MADP = DP.MADP
        AND P2.MANCC = N.MANCC
    )
)
